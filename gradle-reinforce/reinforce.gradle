// è¯»å–é…ç½®æ–‡ä»¶é…ç½®
def reinforcePropertiesFile = rootProject.file("reinforce.properties")
def reinforceProperties = new Properties()
reinforceProperties.load(new FileInputStream(reinforcePropertiesFile))

ext {
    // åŠ å›ºjaråŒ…
    def configReinforceJar = reinforceProperties['reinforce.jar']
    reinforceJar = configReinforceJar != null ? "${project.rootDir}/$configReinforceJar" : "${project.rootDir}/jiagu/jiagu.jar"
    // 360åŠ å›ºè´¦å·
    reinforceAccount = reinforceProperties['reinforce.account']
    // 360åŠ å›ºè´¦å·å¯†ç 
    reinforcePassword = reinforceProperties['reinforce.password']
    // è¾“å‡ºæ–‡ä»¶è·¯å¾„
    def configOutputPath = reinforceProperties['reinforce.outputPath'];
    reinforceOutputPath = configOutputPath != null ? "${project.rootDir}/$configOutputPath" : "${project.buildDir}/outputs/reinforce"

    def configStoreFile = reinforceProperties['sign.storeFile']
    // å¯†é’¥è·¯å¾„
    storeFile = configStoreFile != null ? "${project.rootDir}/$configStoreFile" : null
    // å¯†é’¥å¯†ç 
    storePassword = reinforceProperties['sign.storePassword']
    // å¯†é’¥åˆ«å
    keyAlias = reinforceProperties['sign.keyAlias']
    // åˆ«åå¯†ç 
    keyPassword = reinforceProperties['sign.keyPassword']

    // ç­¾åV2æ£€æµ‹
    def configCheckJar = reinforceProperties['sign.checkV2Jar']
    checkAndroidV2SignatureJar = configCheckJar != null ? "${project.rootDir}/$configCheckJar" : null

    // æ˜¯å¦å¼€å¯ç¾Žå›¢walle
    walleEnable = reinforceProperties['walle.enable'] == 'true'
    // ç¾Žå›¢walleæ‰“åŒ…å·¥å…·
    def configWalleJar = reinforceProperties['walle.jar']
    walleJar = configWalleJar != null ? "${project.rootDir}/$configWalleJar" : "${project.rootDir}/walle-cli-all.jar"

    // ç¾Žå›¢æ‰“åŒ…channelé…ç½®
    def configWalleChannel = reinforceProperties['walle.channel']
    walleChannel = configWalleChannel != null ? "${project.rootDir}/$configWalleChannel" : "${project.rootDir}/walle-cli-all.jar"

    reinforceTmpPath = "${project.buildDir}/reinforceTmpApkPath"
}

def log(content, error = false) {
    def msg = "[reinforce]-> $content"
    if (error) {
        System.err.println msg
    } else {
        System.out.println msg
    }
}


/**
 * æ¸…ç©ºä¸Šä¸€æ¬¡ç”Ÿæˆçš„æ¸ é“åŒ…
 * @param channelApkPath æ¸ é“åŒ…ç›®å½•åœ°å€
 * @return
 */
def cleanFilesPath(String buildType, String flavorName) {
    def outputPath = getReinforceOutputPathByFlavor(buildType, flavorName)
    log "cleanFilesPath : $outputPath"
    delete outputPath

    def tmpPath = getReinforceTmpPathByFlavor(buildType, flavorName)
    log "cleanFilesPath : $tmpPath"
    delete tmpPath

    log "cleanFilesPath : $rootProject.buildDir"
    delete rootProject.buildDir
}

def getReinforceOutputPathByFlavor(String buildType, String flavorName) {
    return getFlavorPath(reinforceOutputPath, buildType, flavorName)
}

def getReinforceTmpPathByFlavor(String buildType, String flavorName) {
    return getFlavorPath(reinforceTmpPath, buildType, flavorName)
}

/**
 * ç”Ÿæˆäº§å“é£Žå‘³è·¯å¾„
 * @param path
 * @param flavorName
 * @return
 */
static def getFlavorPath(String path, String buildType, String flavorName) {
    def file = new File(path, buildType)
    if (!file.exists()) {
        file.mkdirs()
    }
    if (flavorName == null) {
        return file.absolutePath
    }
    file = new File(file, flavorName)
    if (!file.exists()) {
        file.mkdirs()
    }
    return file.absolutePath
}

/**
 * èŽ·å–android sdkç›®å½•
 * @return è¿”å›žsdkä¸‹build-tools é¡¹ç›®ä½¿ç”¨ç‰ˆæœ¬çš„ç›®å½•
 */
String getAndroidSdkPath() {
    File sdkDir = android.getSdkDirectory()
    return sdkDir.getAbsolutePath() + "/build-tools/${rootProject.ext.buildToolsVersion}/"
}

/**
 * å°è£…å‘½ä»¤æ‰§è¡Œæ–¹æ³•
 * @param cmdStr
 * @return
 */
def execCommand(String cmdStr, OutputStream outputStream = System.out) {
    def isWindows = System.properties['os.name'].contains("Windows")
    // Mac bash, Windows cmd, Linux sh
    def cmdTool = isWindows ? "cmd" : "sh"
    def arg = isWindows ? "/c" : "-c"

    exec {
        ExecSpec execSpec ->
            executable cmdTool
            args arg, cmdStr
            standardOutput = outputStream
    }
}

/**
 * 360åŠ å›º
 * @param apk åŠ å›ºçš„åŽŸå§‹apk File
 * @param outputPath è¾“å‡ºç›®å½•
 */
def reinforceApk(File apk, outputPath) {
    if (reinforceAccount == null || reinforcePassword == null) {
        println "reinforceApk throw exception and forced stop!", true
        throw new RuntimeException("360 reinforceApk: please config 360 account name and password first!")
    }
    log "reinforce apk: $apk"
    if (apk == null || !apk.exists()) {
        println "reinforceApk throw exception and forced stop!", true
        throw new FileNotFoundException('apk is not exists and cannot reinforce')
    }
    // jiagu.dbä¸­ç¼“å­˜äº†å¤šæ¸ é“ä¿¡æ¯ï¼Œå¦‚æžœä¸åˆ é™¤ä¼šåˆå¹¶åˆ°å½“å‰å¤šæ¸ é“é…ç½®
    def db = new File("${project.rootDir}/jiagu/jiagu.db")
    if (db.exists()) {
        if (!db.delete()) {
            log "reinforceApk delete jiagu.db error !", true
            throw new RuntimeException("reinforceApk delete jiagu.db failure!")
        }

    }
    def file = new File(outputPath)
    if (!file.exists()) {
        file.mkdir()
    }
    execCommand "java -jar $reinforceJar -version"
    execCommand "java -jar $reinforceJar -login $reinforceAccount $reinforcePassword"
    execCommand "java -jar ${reinforceJar} -showsign"
    execCommand "java -jar ${reinforceJar} -jiagu ${apk} ${outputPath}"

    log "--- reinforce end! ---"
}

/**
 * åŠ å›ºåŽçš„apk å¯¹é½åŽ‹ç¼©
 * @param apk å·²åŠ å›ºapk
 * @return è¿”å›žå¯¹é½åŽ‹ç¼©åŽçš„apk
 */
def zipAlignApk(File apk) {
    if (apk == null || !apk.exists()) {
        log "zipalign reinforceApk throw exception and forced stop!", true
        throw new FileNotFoundException('apk is not exists and cannot reinforce')
    }
    def buildToolPath = getAndroidSdkPath()
    def outFilePath = "${apk.parent}/${apk.name.replaceAll(".apk", "_zip.apk")}"
    def file = new File(outFilePath)
    if (file.exists()) {
        file.delete()
    }
    execCommand "${buildToolPath}zipalign -v -p 4 ${apk} ${outFilePath}"

    return new File(outFilePath)
}

/**
 * å¯¹apkç­¾å
 * @param zipApk åŽ‹ç¼©å¯¹é½åŽçš„apk
 * @return ç­¾ååŽçš„apk
 */
def signApkV2(File zipApk, String buildType) {
    if (zipApk == null || !zipApk.exists()) {
        log "sign zipApk throw exception and forced stop!", true
        throw new FileNotFoundException('apk is not exists and cannot reinforce')
    }
    def buildToolPath = getAndroidSdkPath()
    def outFilePath = "${zipApk.parent}/${zipApk.name.replaceAll(".apk", "_sign.apk")}"

    def file = new File(outFilePath)
    if (file.exists()) {
        file.delete()
    }

    def buildTypeSignConfig = project.android.buildTypes.findByName(buildType).signingConfig
    log "sign zipApk signConfig: $buildTypeSignConfig"
    def keyPath = storeFile ?: buildTypeSignConfig.storeFile
    def storePassword = storePassword ?: buildTypeSignConfig.storePassword
    def keyAlias = keyAlias ?: buildTypeSignConfig.keyAlias
    def keyPassword = keyPassword ?: buildTypeSignConfig.keyPassword

    execCommand "${buildToolPath}apksigner sign --ks ${keyPath} " +
            "--ks-key-alias ${keyAlias} " +
            "--ks-pass pass:${storePassword} " +
            "--key-pass pass:${keyPassword} " +
            "--out ${outFilePath} ${zipApk}"

    return new File(outFilePath)
}

def checkAndroidV2Signature(File apk) {
    if (apk == null || !apk.exists()) {
        log "checkAndroidV2Signature throw exception and forced stop!", true
        throw new FileNotFoundException('apk is not exists and cannot check Android V2 Signature')
    }

    def output = new ByteArrayOutputStream()
    execCommand "java -jar $checkAndroidV2SignatureJar ${apk.path}", output
    log "check Android V2 Signature result: ${output.toString()}"
}

/**
 * å¯¹ç­¾ååŽçš„apkæ·»åŠ æ¸ é“ä¿¡æ¯
 * @param apk å·²ç­¾åapk
 * @return æ·»åŠ æ¸ é“ä¿¡æ¯åŽçš„apk
 */
def buildChannelApks(File apk, String outputPath) {
    if (apk == null || !apk.exists()) {
        log "channel build Apk throw exception and forced stop!", true
        throw new FileNotFoundException('apk is not exists and cannot reinforce')
    }
    log "walle write channel start"
    def file = new File(outputPath)
    if (!file.exists()) {
        file.mkdir()
    }
    execCommand "java -jar ${walleJar} batch -f ${walleChannel} ${apk} ${outputPath}"
    log "walle write channel over"
}

/**
 * é‡å‘½åapk
 * @param path æ¸ é“apkç›®å½•è·¯å¾„
 * @return
 */
def renameChannelApkFiles(path) {
    def regex = "_jiagu_zip_sign"
    def dir = new File(path + "/")
    dir.listFiles().each { file ->
        if (file.name =~ /.*${regex}.*\.apk/) {
            String newName = file.name
            newName = newName.replaceAll(~/_zip/, "")
            file.renameTo(new File(file.getParent(), newName))
        }
    }
}

def buildReinforceOutPutFile(File apk, String outputPath) {
    if (apk == null || !apk.exists()) {
        log "build Apk throw exception and forced stop!", true
        throw new FileNotFoundException('apk is not exists and cannot reinforce output')
    }
    def file = new File(outputPath)
    if (!file.exists()) {
        file.mkdir()
    }
    apk.renameTo(new File(file, apk.name))
}

/**
 * æŸ¥æ‰¾apk
 * @param path
 * @param suffix
 * @return
 */
static File findApkFile(path, suffix) {
    def dir = new File(path)
    return dir.listFiles().find { it.isFile() && it =~ /.*${suffix}\.apk/ }
}

def renameByGitInfo(File apk) {
    def outputStream = new ByteArrayOutputStream()
    execCommand "git rev-parse --short HEAD", outputStream
    def newName = apk.name.replaceAll(".apk", "_${outputStream.toString().trim()}.apk")
    def newFile = new File(apk.getParent(), newName)
    if (!apk.renameTo(newFile)) {
        log 'rename By Git Info failed !', true
        return apk
    }
    return newFile
}

/**
 * åŠ å›º -> zipå¯¹é½ -> V2ç­¾å -> ç­¾åæ ¡éªŒ ->
 * @param flavorName
 * @return
 */
def doReinforce(String buildType, String flavorName = null) {
    cleanFilesPath(buildType, flavorName)   // æ¸…ç©ºä¸Šä¸€æ¬¡ç”Ÿæˆçš„æ¸ é“åŒ…
    def defaultApkPath = (flavorName == null || flavorName.isEmpty())
            ? "${project.buildDir}/outputs/apk/$buildType"
            : "${project.buildDir}/outputs/apk/${flavorName}/$buildType"
    def releaseApkFile = findApkFile(defaultApkPath, '')  //éåŽ†æ–‡ä»¶ï¼Œå¯»æ‰¾releaseåŒ…
    log "1_release: $releaseApkFile"
    if (releaseApkFile != null) {
        def tmpOutPath = getReinforceTmpPathByFlavor(buildType, flavorName)
        reinforceApk(releaseApkFile, tmpOutPath)   //æ‰§è¡ŒåŠ å›º
        def reinforceApk = findApkFile(tmpOutPath, "_jiagu")  //å¯»æ‰¾å·²åŠ å›ºçš„apkåŒ…
        log "2_reinforce: reinforceApk"
        if (reinforceApk != null) {
            def zipAlignApk = zipAlignApk(reinforceApk) // zipå¯¹é½
            log "3_zipAlign: $zipAlignApk"
            if (zipAlignApk != null) {
                def signatureApk = signApkV2(zipAlignApk, buildType)  //ä½¿ç”¨V2é‡ç­¾å
                log "4_signV2: $signatureApk"
                if (signatureApk != null) {
                    if (checkAndroidV2SignatureJar != null) {
                        checkAndroidV2Signature(signatureApk) // ä½¿ç”¨V2ç­¾åæ£€æŸ¥å·¥å…·æ£€æŸ¥
                        log "5_checkSignV2: $signatureApk"
                    } else {
                        log "5_checkSignV2: not config check V2 Signature Jar, so ignore !", true
                    }
                    def targetApk = renameByGitInfo(signatureApk)
                    log "6_addGitCommit: $targetApk"
                    def reinforceOutPath = getReinforceOutputPathByFlavor(buildType, flavorName)
                    if (walleEnable) {
                        buildChannelApks(targetApk, reinforceOutPath)  //æ‰§è¡Œå¤šæ¸ é“æ‰“åŒ…
                        renameChannelApkFiles(reinforceOutPath) //é‡å‘½åæ¸ é“åŒ…
                    } else {
                        buildReinforceOutPutFile(targetApk, reinforceOutPath)
                    }
                    log """
                        ðŸŽ‰ðŸŽ‰ðŸŽ‰ðŸŽ‰ åŠ å›ºç­¾åå®Œæˆ ðŸŽ‰ðŸŽ‰ðŸŽ‰ðŸŽ‰ 
                        ðŸ‘‡ðŸ‘‡ðŸ‘‡ðŸ‘‡
                        è¾“å‡ºè·¯å¾„ï¼š$reinforceOutPath
                        ðŸ‘†ðŸ‘†ðŸ‘†ðŸ‘†
                        ðŸŽ‰ðŸŽ‰ðŸŽ‰ðŸŽ‰ åŠ å›ºç­¾åå®Œæˆ ðŸŽ‰ðŸŽ‰ðŸŽ‰ðŸŽ‰ 
                        """
                }
            }
        }
    }
}

/**
 * åˆ›å»ºåŠ å›ºtask
 */
android.applicationVariants.all { variant ->
    String flavorName = variant.getFlavorName()
    String buildType = variant.buildType.name
    // åŠ å›ºä»»åŠ¡
    if (flavorName == null) {
        task "assembleReinforce${buildType.capitalize()}" {
            group 'reinforce'
            dependsOn("assembleRelease")
            doLast {
                doReinforce buildType
            }
        }
    } else {
        task "assembleReinforce${flavorName.capitalize()}${buildType.capitalize()}" {
            group 'reinforce'
            dependsOn("assemble${flavorName.capitalize()}${buildType.capitalize()}")
            doLast {
                doReinforce buildType, flavorName
            }
        }
    }
}